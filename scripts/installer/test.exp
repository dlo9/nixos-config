# This is an interactive script to test the partitioning and
# installer scripts in a VM before I try them on a real host.
#
# This script:
#   - Asks the user for basic test parameters
#   - Creates disks to attach to the VM
#   - Downloads the Nix 22.05 minimal installer ISO
#   - Boots the VM
#   - Copies partitioner and installer scripts
#   - Executes the partitioner and installer

###################
##### Prelude #####
###################

# Include library functions and set a log file
source lib.exp
log_file test.log

######################
##### Parameters #####
######################

# TODO: Prompt the user for these and set a default
set vm_mem $env(vm_mem)
set installer_iso $env(installer_iso)
set bios $env(bios)
set local_ssh_port $env(local_ssh_port)
set extra_args [split $env(extra_args) " "]

set state_dir "test-state"

# TODO: Create disks, download installer

###################
##### Boot VM #####
###################

spawn qemu-kvm \
	-m "$vm_mem" \
	-nographic \
	-cdrom "$installer_iso" \
	-bios "$bios" \
	-nic "user,hostfwd=tcp::$local_ssh_port-:22" {*}$extra_args
set qemuId $spawn_id

# Wait for grub, then select the right menu entry
set down "\[B"
expect {
  # Bios
  "Automatic boot in" {
    # Go down four lines to the serial console entry
    send [string repeat $down 4]
    expect "(serial console=ttyS0"
  }

  # EFI
  "will be executed automatically in" {

    # Select submenu
    send [string repeat $down 4]
    expect "*HiDPI, Quirks and Accessibility"
    sleep 1
    send "\r"

    # Wait for submenu to load
    expect "ESC to return"

    # Select submenu
    send [string repeat $down 10]
    expect "*Serial console=ttyS0"
    sleep 1
    send "\r"

    # Wait for submenu to load
    expect "ESC to return"
  }
}

# Select menu entry
sleep 1
send "\r"

# Wait for prompt after boot
expect "<<< NixOS Stage 1 >>>"
expect "<<< NixOS Stage 2 >>>"
expect -timeout 60 "Welcome to NixOS"
# Set console size
stty rows 150 cols 200
expectPrompt

# Set password for easy SSH
send "passwd\r"
expect "New password:"
send "nixos\r"
expect "Retype new password:"
send "nixos\r"
expect "password updated successfully"
expectPrompt

########################
##### Copy Scripts #####
########################

sleep 1
send_user "Copying installer via SCP...\r\n"
spawn scp -P 2222 -o "UserKnownHostsFile=/dev/null" -o "StrictHostKeyChecking=no" -r /etc/nixos/scripts/installer/ nixos@localhost:/home/nixos
expect -timeout 30 "Password:"
send "nixos\r"
expectOrError eof

# Reattach to qemu
send_user "Returning control to QEMU\r\n"
set spawn_id $qemuId
send "\r"

# Give user control
# Return control with EOF (Ctrl-D) # TODO: Or not?? This doesn't work
#interact

###################
##### Install #####
###################

source partition.exp
source install.exp
