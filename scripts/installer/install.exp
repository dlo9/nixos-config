#!/usr/bin/env -S expect -f

# Installs NixOS to $installDir

###################
##### Prelude #####
###################

# Include library functions and set a log file
source lib.exp
log_file install.log

######################
##### Parameters #####
######################

source inputs.exp
requireVars rootPassword configDir installDir hostname masterSopsAgeKey disks

#####################
##### Functions #####
#####################

proc copyConfig {configDir installDir} {
    _info "Copying config to target..."
    file mkdir $installDir/etc/nixos
    exec rsync -a --progress --delete --delete-excluded --exclude="*.log" $configDir/ $installDir/etc/nixos
}

proc setHostname {hostname} {
    _info "Setting hostname to $hostname..."
    exec hostname $hostname
}

proc generateHardwareConfig {hostname installDir configDir} {
    _info "Generating hardware config..."
    exec nixos-generate-config --root "$installDir" --show-hardware-config > "$configDir/hardware/$hostname.nix"
    exec chown nixos:users "$configDir/hardware/$hostname.nix"
}

proc installInitSshKey {hostname masterSopsAgeKey configDir installDir} {
    _info "Installing init ssh key..."
    exec env "SOPS_AGE_KEY=$masterSopsAgeKey" sops -d --extract \["ssh-keys"\]\["host"\]\["rsa"\] "$configDir/hosts/$hostname/secrets.yaml" > $installDir/var/ssh_host_rsa_key
}

proc installAgeKey {hostname masterSopsAgeKey configDir installDir} {
    _info "Installing age key..."
    exec env "SOPS_AGE_KEY=$masterSopsAgeKey" sops -d --extract \["$hostname"\]\["private"\] $configDir/sys/secrets/age-keys.yaml > $installDir/var/sops-age-keys.txt
}

proc install {hostname installDir rootPassword} {
    _info "Installing..."
    # The scheme must be `path://`, or else `git+file://` is used, which doesn't add the untracked hardware config to the store
    # Root password is set in flake, plus there's a prompting issue: https://github.com/NixOS/nixpkgs/issues/95778
    spawn nixos-install --no-root-password --flake "path://$installDir/etc/nixos#$hostname" --root $installDir
    expect -timeout -1 eof
}

#################
##### Start #####
#################

_info "##### Starting Install #####"

requireUser "root"

generateHardwareConfig $hostname $installDir $configDir
copyConfig $configDir $installDir
setHostname $hostname
installInitSshKey $hostname $masterSopsAgeKey $configDir $installDir
installAgeKey $hostname $masterSopsAgeKey $configDir $installDir
install $hostname $installDir $rootPassword

_success "##### Finished Install #####"
